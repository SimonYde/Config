#!/usr/bin/env -S nu --no-config-file

use std/log

def on [ ] {
    try {
        hyprctl --batch ([
            "keyword animations:enabled 0",
            "keyword decoration:blur:enabled 0",
            "keyword general:gaps_in 0",
            "keyword general:gaps_out 0",
            "keyword general:border_size 1",
            "keyword decoration:rounding 0",
        ] | str join ";") | ignore
    }
    log info "Disabled power hungry hyprland decorations"

    powerprofilesctl set power-saver
    log info "Set power profile to `power-saver`"
    systemctl --user stop hyprland-autoname-workspaces.service swww.service blueman-applet.service adb-server.service
    log info "Stopped certain user services"
    systemctl stop bluetooth.service syncthing.service
    log info "Disabled Bluetooth"
    set-refresh 60
    log info "Set screen refresh rate to 60"
}

def set-refresh [refresh: int] {
    let monitors = hyprctl monitors -j | from json
    $monitors | each {
        if $in.name =~ eDP {
            hyprctl keyword monitor $"($in.name),($in.width)x($in.height)@($refresh),($in.x)x($in.y),($in.scale)"
        }
    }
}

def off [ ] {
    hyprctl reload
    powerprofilesctl set balanced
    log info "Set power profile to `balanced`"
    systemctl --user start hyprland-autoname-workspaces.service swww.service blueman-applet.service adb-server.service
    log info "Enabled certain user services"
    systemctl start bluetooth.service syncthing.service
    log info "Enabled Bluetooth"
    set-refresh 120
    log info "Set screen refresh rate to 120"
}

def main [state?: string] {
    match $state {
        "on" => {
            on
        }

        "off" => {
            off
        }

        null => {
            on
        }
    }
}
